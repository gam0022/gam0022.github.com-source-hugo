+++
draft = false
tags = [
    "Unity", "Shader", "CG"
]
title = "[Unity][URP] Yè»¸ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼"
slug = "unity-urp-billboard-shader"
date = "2021-12-23T10:00:00+09:00"
image = "/images/posts/2021-12-23-unity-urp-billboard-shader/20211223020601_x1.jpg"
toc = true
math = false

+++

ã“ã‚Œã¯[Unity Advent Calendar 2021](https://qiita.com/advent-calendar/2021/unity)ã®23æ—¥ç›®ã®è¨˜äº‹ã§ã™ã€‚

å‰æ—¥ã¯[@UnagiHuman](https://twitter.com/UnagiHuman)ã•ã‚“ã®[ã€ŒUnityã®æ–°MeshAPIã§MeshColliderã‚’ãƒªã‚¢ãƒ«ã‚¿ã‚¤ãƒ å¤‰å½¢ã•ã›ã‚‹ã€](https://qiita.com/UnagiHuman/items/7db6c75adea0d5862acf)ã§ã—ãŸã€‚

---

ã“ã‚“ãªæ„Ÿã˜ã®Yè»¸ã®ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰ã‚’C#ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’ä½¿ã‚ãšã«ã€ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã ã‘ã§å®Ÿè£…ã—ã¾ã—ãŸã€‚

![Yè»¸ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰](/images/posts/2021-12-23-unity-urp-billboard-shader/Unity-URP-Billboard-trim.gif)

GitHubãƒªãƒã‚¸ãƒˆãƒª: [gam0022/unity-urp-shader](https://github.com/gam0022/unity-urp-shader)

# è¦ç´„

- ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã ã‘ã§Yè»¸ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰
- Unityã®URPå¯¾å¿œ
- å›è»¢è¡Œåˆ—ã‚’ç”Ÿæˆã™ã‚‹ã‚¢ãƒ—ãƒ­ãƒ¼ãƒãªã®ã§ã€ãƒ—ãƒ©ãƒƒãƒˆãƒ•ã‚©ãƒ¼ãƒ é–“ã®é•ã„ï¼ˆZã®æ–¹å‘ã‚„UVã®ä¸Šä¸‹ãªã©ï¼‰ã«ã‚ˆã‚‹å•é¡ŒãŒèµ·ããªã„

# å‰å›ã®è¨˜äº‹ã¨ã®é•ã„

ã“ã®è¨˜äº‹ã¯[ä»¥å‰ã®è¨˜äº‹](/blog/2019/07/23/unity-y-axis-billboard-shader/)ã®æ”¹è¨‚ç‰ˆã§ã™ã€‚

- URPï¼ˆUniversal Render Pipelineï¼‰ã«å¯¾å¿œ
- ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰å‡¦ç†ã®ã‚¢ãƒ—ãƒ­ãƒ¼ãƒã‚’æ”¹è‰¯ï¼ˆãƒ—ãƒ©ãƒƒãƒˆãƒ•ã‚©ãƒ¼ãƒ ä¾å­˜ã‚’ãªãã™ï¼‰

<!--more-->

# ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§å®Ÿè£…ã™ã‚‹ãƒ¡ãƒªãƒƒãƒˆ

ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰ã‚’è¨ˆç®—ã™ã‚‹ãƒ¡ãƒªãƒƒãƒˆã¯ãŸãã•ã‚“ã‚ã‚Šã¾ã™ã€‚

- C#ã‚¹ã‚¯ãƒªãƒ—ãƒˆãŒä¸è¦ã§ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã ã‘ã§å‹•ä½œã™ã‚‹
    - ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ãƒãƒ¼ã‚¿ãƒ“ãƒªãƒ†ã‚£ã¯é«˜ã„ï¼
    - ã‹ã¤ã¦ã®VRCã®ã‚ˆã†ã«ãƒ¦ãƒ¼ã‚¶ã‚¹ã‚¯ãƒªãƒ—ãƒˆãŒæ›¸ã‘ãªã„ç’°å¢ƒã§ã‚‚ä½¿ãˆã‚‹
- ã‚·ãƒ¼ãƒ³ãƒ“ãƒ¥ãƒ¼ä¸Šã§ã‚‚å‹•ä½œã™ã‚‹
- GPUï¼ˆé ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ï¼‰ã§ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰è¨ˆç®—ãŒã§ãã‚‹
    - ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰è¨ˆç®—ã®ãŸã‚ã®CPUè² è·ãŒ0
    - æ¿ãƒãƒªã®é ‚ç‚¹æ•°ã¯4ãªã®ã§ã€é ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§å¤šå°‘é‡ã„å‡¦ç†ã‚’ã—ã¦ã‚‚ã€GPUè² è·ã¸ã®å½±éŸ¿ã¯ã‚ãšã‹

# ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ã‚³ãƒ¼ãƒ‰ï¼ˆå…¨ä½“ï¼‰

æœ€çµ‚çš„ãªã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ã‚³ãƒ¼ãƒ‰ã¯ã“ã¡ã‚‰ã§ã™ã€‚

å˜ä½“ã§å‹•ä½œã™ã‚‹ã®ã§ã€ã‚³ãƒ”ãƒšã—ã¦ä½¿ãˆã¾ã™ã€‚MITãƒ©ã‚¤ã‚»ãƒ³ã‚¹ã§ã™ã€‚

```c
// URP-Unlit-Billboard Shader by @gam0022 (MIT Licence)
// https://gam0022.net//blog/2021/12/23/unity-urp-billboard-shader/
Shader "Universal Render Pipeline/Unlit-Billboard"
{
    Properties
    {
        _BaseMap ("Base Map", 2D) = "white" { }
        _BaseColor ("Base Color", Color) = (1, 1, 1, 1)
        _Cutoff ("Alpha Cutoff", Range(0, 1)) = 0.5
    }

    SubShader
    {
        Tags {
            "RenderPipeline" = "UniversalPipeline"
            "RenderType" = "TransparentCutout"
            "Queue" = "AlphaTest"
            "IgnoreProjector" = "True"
        }

        Pass
        {
            Tags { "LightMode" = "UniversalForward" }

            HLSLPROGRAM

            #pragma vertex vert
            #pragma fragment frag

            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"

            struct Attributes
            {
                float4 positionOS: POSITION;
                float2 uv: TEXCOORD0;
            };

            struct Varyings
            {
                float4 positionHCS: SV_POSITION;
                float2 uv: TEXCOORD0;
            };

            sampler2D _BaseMap;

            CBUFFER_START(UnityPerMaterial)
            float4 _BaseMap_ST;
            half4 _BaseColor;
            half _Cutoff;
            CBUFFER_END

            Varyings vert(Attributes IN)
            {
                Varyings OUT;

                // å›è»¢è¡Œåˆ—ã‚’ç”Ÿæˆã—ã¦ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰å‡¦ç†ã‚’ã—ã¾ã™
                float3 yup = float3(0.0, 1.0, 0.0);
                float3 up = mul((float3x3)unity_ObjectToWorld, yup);

                float3 worldPos = unity_ObjectToWorld._m03_m13_m23;
                float3 toCamera = _WorldSpaceCameraPos - worldPos;
                float3 right = normalize(cross(toCamera, up));
                float3 forward = normalize(cross(up, right));

                float4x4 mat = {
                    1, 0, 0, 0,
                    0, 1, 0, 0,
                    0, 0, 1, 0,
                    0, 0, 0, 1,
                };
                mat._m00_m10_m20 = right;
                mat._m01_m11_m21 = up;
                mat._m02_m12_m22 = forward;
                mat._m03_m13_m23 = worldPos;

                float4 vertex = float4(IN.positionOS.xyz, 1);
                vertex = mul(mat, vertex);
                OUT.positionHCS = mul(UNITY_MATRIX_VP, vertex);

                OUT.uv = TRANSFORM_TEX(IN.uv, _BaseMap);
                return OUT;
            }

            half4 frag(Varyings IN): SV_Target
            {
                half4 base = tex2D(_BaseMap, IN.uv);
                clip(base.a - _Cutoff);
                return base * _BaseColor;
            }
            ENDHLSL

        }
    }
}
```

# è§£èª¬

## å‰å›ã®è¨˜äº‹ï¼ˆãƒ“ãƒ¥ãƒ¼å¤‰æ›ã‚’ã‚¹ã‚­ãƒƒãƒ—ï¼‰ã®æ¬ ç‚¹

[ä»¥å‰ã®è¨˜äº‹](/blog/2019/07/23/unity-y-axis-billboard-shader/)ã§ã¯ã€ã‚«ãƒ¡ãƒ©ã®ãƒ“ãƒ¥ãƒ¼è¡Œåˆ—ã®å¤‰æ›ã‚’ã‚¹ã‚­ãƒƒãƒ—ã™ã‚‹ã“ã¨ã§ã€ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰å‡¦ç†ã‚’å®Ÿè£…ã—ã¦ã„ã¾ã—ãŸã€‚

Unityã§ã¯ãƒ—ãƒ©ãƒƒãƒˆãƒ•ã‚©ãƒ¼ãƒ é–“ã®é•ã„ï¼ˆZã®æ–¹å‘ã‚„UVã®ä¸Šä¸‹ãªã©ï¼‰ã‚’ãƒ“ãƒ¥ãƒ¼è¡Œåˆ—ã§ã†ã¾ãå¸åã™ã‚‹è¨­è¨ˆã«ãªã£ã¦ãŠã‚Šã€ãƒ“ãƒ¥ãƒ¼è¡Œåˆ—ã®å¤‰æ›ã‚’ã‚¹ã‚­ãƒƒãƒ—ã™ã‚‹ã¨ãƒ—ãƒ©ãƒƒãƒˆãƒ•ã‚©ãƒ¼ãƒ ã®å¯¾å¿œã‚’è‡ªåŠ›ã§è¡Œã†å¿…è¦ãŒå‡ºã¦ãã¦ã€ã‹ãªã‚Šé¢å€’ã§ã—ãŸã€‚
é¢å€’ãªã ã‘ãªã‚‰ã¾ã ã„ã„ã®ã§ã™ãŒã€å°†æ¥çš„ã«æ–°ã—ã„ãƒ—ãƒ©ãƒƒãƒˆãƒ•ã‚©ãƒ¼ãƒ ãŒå¢—ãˆãŸæ™‚ãªã©ã«ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ä¿®æ­£ãŒå¿…è¦ã«ãªã‚‹å¯èƒ½æ€§ã‚‚ã‚ã‚Šã€ã“ã®ã‚¢ãƒ—ãƒ­ãƒ¼ãƒã¯ç­‹ãŒè‰¯ããªã„ãªã€ã¨è¨˜äº‹ã®å…¬é–‹å¾Œã«æ€ã£ã¦ã„ã¾ã—ãŸã€‚

ã“ã®è¨˜äº‹ã§ç´¹ä»‹ã™ã‚‹ **å›è»¢è¡Œåˆ—ã‚’ç”Ÿæˆã™ã‚‹ã‚¢ãƒ—ãƒ­ãƒ¼ãƒ** ã§ã¯ã€ãã®ã‚ˆã†ãªé¢å€’ãªå•é¡ŒãŒèµ·ãã¾ã›ã‚“ã€‚

## å›è»¢è¡Œåˆ—ã‚’ç”Ÿæˆã™ã‚‹ã‚¢ãƒ—ãƒ­ãƒ¼ãƒ

UnityãŒç”Ÿæˆã™ã‚‹ãƒ¢ãƒ‡ãƒ«è¡Œåˆ—ã‚’ä½¿ã‚ãšã«ã€é ‚ç‚¹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã®ä¸­ã§ã†ã¾ãå›è»¢è¡Œåˆ—ã‚’ç”Ÿæˆã™ã‚‹ã“ã¨ã§ã€ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰å‡¦ç†ã‚’å®Ÿç¾ã—ã¾ã™ã€‚

ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‹ã‚‰ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰å‡¦ç†ã‚’æŠœãå‡ºã—ã¦ã€ã§ãã‚‹ã ã‘ã‚³ãƒ¡ãƒ³ãƒˆã‚’å…¥ã‚Œã¾ã—ãŸã€‚

```c
// å›è»¢è¡Œåˆ—ã‚’ç”Ÿæˆã—ã¦ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰å‡¦ç†ã‚’ã—ã¾ã™

// Y-UPãƒ™ã‚¯ãƒˆãƒ«
float3 yup = float3(0.0, 1.0, 0.0);

// up = Yè»¸ã®åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«
// ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®Transformã®å›è»¢ã‚’è€ƒæ…®ã™ã‚‹
float3 up = mul((float3x3)unity_ObjectToWorld, yup);

// ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã®ãƒ¯ãƒ¼ãƒ«ãƒ‰åº§æ¨™
float3 worldPos = unity_ObjectToWorld._m03_m13_m23;

// ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‹ã‚‰ã‚«ãƒ¡ãƒ©ã«å‘ã‹ã†ãƒ™ã‚¯ãƒˆãƒ«
float3 toCamera = _WorldSpaceCameraPos - worldPos;

// right = Xè»¸ã®åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«
// rightã¯toCameraã¨upã®ä¸¡æ–¹ã«ç›´äº¤ã™ã‚‹ã®ã§ã€crossã‹ã‚‰è¨ˆç®—ã§ãã‚‹
float3 right = normalize(cross(toCamera, up));

// forward = Zè»¸ã®åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«
// forwardã¯upã¨rightã®ä¸¡æ–¹ã«ç›´äº¤ã™ã‚‹ãƒ™ã‚¯ãƒˆãƒ«
float3 forward = normalize(cross(up, right));

// å„åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«ã‚’ä¸¦ã¹ã¦ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰ç”¨ã®å›è»¢è¡Œåˆ—ã‚’ç”Ÿæˆ
// ï¼ˆå³å¯†ã«ã¯å¹³è¡Œç§»å‹•ã‚‚å«ã‚“ã§ã„ã‚‹ã®ã§å§¿å‹¢è¡Œåˆ—ï¼‰
float4x4 mat = {
    1, 0, 0, 0,
    0, 1, 0, 0,
    0, 0, 1, 0,
    0, 0, 0, 1,
};
mat._m00_m10_m20 = right;//     Xè»¸ã®åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«
mat._m01_m11_m21 = up;//        Yè»¸ã®åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«
mat._m02_m12_m22 = forward;//   Zè»¸ã®åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«
mat._m03_m13_m23 = worldPos;//  å¹³è¡Œç§»å‹•ã®ãƒ™ã‚¯ãƒˆãƒ«


// ãƒ­ãƒ¼ã‚«ãƒ«åº§æ¨™ï¼ˆå¹³è¡Œç§»å‹•ã®ãŸã‚ã«w=1ï¼‰
float4 vertex = float4(IN.positionOS.xyz, 1);

// ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰ç”¨ã®å›è»¢è¡Œåˆ—ã‚’ä¹—ç®—ã—ã¦ãƒ¯ãƒ¼ãƒ«ãƒ‰ç©ºé–“ã«å¤‰æ›
vertex = mul(mat, vertex);

// ãƒ“ãƒ¥ãƒ¼è¡Œåˆ—ã¨ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ã‚·ãƒ§ãƒ³è¡Œåˆ—ã‚’ä¹—ç®—ã—ã¦ã‚¯ãƒªãƒƒãƒ—ç©ºé–“ã«å¤‰æ›
OUT.positionHCS = mul(UNITY_MATRIX_VP, vertex);
```

ã“ã‚Œã¯è¶…é‡è¦æƒ…å ±ã§ã™ãŒã€ **å›è»¢å¾Œã®ç©ºé–“ã®åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«ã‚’ä¸¦ã¹ãŸè¡Œåˆ—ãŒå›è»¢è¡Œåˆ—ã«ãªã‚Šã¾ã™ã€‚** 

ã“ã‚Œã ã‘è¦šãˆã¦ãŠã‘ã°ã€å›è»¢ã ã‘ã§ãªãã€æ‹¡å¤§ç¸®å°ã‚„Skewï¼ˆã›ã‚“æ–­ï¼‰ã®è¡Œåˆ—ã¯è‡ªç„¶ã«å°å‡ºã§ãã¾ã™ã€‚

çŸ¥ã‚‰ãªã‹ã£ãŸäººã¯ãœã²è¦šãˆã¦ãŠãã¾ã—ã‚‡ã†ã€‚CEDECã§åŒã˜è©±ã‚’2å›ãã‚‰ã„ã—ã¦ã„ã¾ã™ï¼ˆ[2020](https://www.klab.com/jp/blog/creative/2020/cedec2020.html)ã¨[2021](https://www.klab.com/jp/blog/tech/2021/cedec-kyushu-2021-online-3d.html)ï¼‰ã€‚

![åŸºåº•ãƒ™ã‚¯ãƒˆãƒ«ã‚’ã‚¤ãƒ¡ãƒ¼ã‚¸ã™ã‚Œã°OK](/images/posts/2021-12-23-unity-urp-billboard-shader/kec2-1.png)
![å›è»¢ãƒ™ã‚¯ãƒˆãƒ«1](/images/posts/2021-12-23-unity-urp-billboard-shader/kec2-2.png)
![å›è»¢ãƒ™ã‚¯ãƒˆãƒ«2](/images/posts/2021-12-23-unity-urp-billboard-shader/kec2-3.png)

## SRP Batcher

URPï¼ˆSRPï¼‰ã‹ã‚‰SRP Batcherã¨ã„ã†ãƒ‰ãƒ­ãƒ¼ã‚³ãƒ¼ãƒ«ãƒãƒƒãƒãƒ³ã‚°ï¼ˆå³å¯†ã«ã¯ãƒ‰ãƒ­ãƒ¼ã‚³ãƒ¼ãƒ«ã®æ•°ã‚’æ¸›ã‚‰ã™ã‚ã‘ã§ã¯ãªãã€ãƒ‰ãƒ­ãƒ¼ã‚³ãƒ¼ãƒ«é–“ã®GPUã®è¨­å®šã‚³ã‚¹ãƒˆã‚’å‰Šæ¸›ï¼‰ã®ä»•çµ„ã¿ãŒå°å…¥ã•ã‚Œã¾ã—ãŸã€‚

ä»¥å‰ã®ãƒ“ãƒ«ãƒ‰ã‚¤ãƒ³ãƒ¬ãƒ³ãƒ€ãƒ¼ãƒ‘ã‚¤ãƒ—ãƒ©ã‚¤ãƒ³ã®ãƒ‰ãƒ­ãƒ¼ã‚³ãƒ¼ãƒ«ãƒãƒƒãƒãƒ³ã‚°ã§ã¯MeshãŒçµåˆã•ã‚Œã‚‹ã®ã§ã€ãƒ“ãƒ«ãƒœãƒ¼ãƒ‰ã®ã‚ˆã†ã«ç‰¹æ®Šãªé ‚ç‚¹å¤‰æ›ã‚’ã™ã‚‹ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã§ã¯è€ƒæ…®ãŒå¿…è¦ã§ã€ã‘ã£ã“ã†é¢å€’ã§ã—ãŸã€‚

SRP Batcherã¯Meshã‚’çµåˆã—ãªã„ã®ã§ã€é ‚ç‚¹å¤‰æ›ã§ç‰¹åˆ¥ãªè€ƒæ…®ã‚’ã—ãªãã¦ã‚‚ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ãŒå‹•ãã‚ˆã†ã«ãªã‚Šã¾ã—ãŸï¼ã‚ã§ãŸã„ğŸ‰

ä»Šå›ã®ã‚·ã‚§ãƒ¼ãƒ€ãƒ¼ã‚’ãƒ•ãƒ¬ãƒ¼ãƒ ãƒ‡ãƒãƒƒã‚¬ãƒ¼ã§ç¢ºèªã™ã‚‹ã¨ã€ã¡ã‚ƒã‚“ã¨SRP Batcherã§æç”»ã•ã‚Œã¦ã„ã‚‹ã®ãŒåˆ†ã‹ã‚Šã¾ã™ã€‚

[![SRP Batcher](/images/posts/2021-12-23-unity-urp-billboard-shader/frame-debugger.png)](/images/posts/2021-12-23-unity-urp-billboard-shader/frame-debugger.png)

SRP Batcherã«ã¤ã„ã¦ã¯ã€ä»¥ä¸‹ã®è¨˜äº‹ãŒè©³ã—ã„ã§ã™ã€‚

- [SRP Batcherï¼šãƒ¬ãƒ³ãƒ€ãƒªãƒ³ã‚°ã‚’ã‚¹ãƒ”ãƒ¼ãƒ‰ã‚¢ãƒƒãƒ— | Unity Blog](https://blog.unity.com/ja/technology/srp-batcher-speed-up-your-rendering)

# ãƒªãƒ³ã‚¯

å‚è€ƒã«ã•ã›ã¦ã„ãŸã ãã¾ã—ãŸã€‚ã‚ã‚ŠãŒã¨ã†ã”ã–ã„ã¾ã™ã€‚

- URPå¯¾å¿œï¼ˆSRP Batcherã‚‚å¯¾å¿œğŸ‘ï¼‰
    - [ã€Unityã€‘URPç”¨ã®ã‚·ã‚§ãƒ¼ãƒ€ã®æ›¸ãæ–¹ãŒæ—§ãƒ‘ã‚¤ãƒ—ãƒ©ã‚¤ãƒ³ã¨å¾®å¦™ã«é•ã£ã¦ã‚„ã‚„ã“ã—ã„ã®ã§ã¾ã¨ã‚ãŸ - LIGHT11](https://light11.hatenadiary.com/entry/2021/07/29/194213)
- åˆ©ç”¨ã—ãŸãƒ†ã‚¯ã‚¹ãƒãƒ£ç´ æ
    - [Tree PNG Clipart Background](http://www.pngall.com/tree-png/download/23754)
    - [Dirt/Ground Texture [Tileable | 2048x2048]](https://www.deviantart.com/fabooguy/art/Dirt-Ground-Texture-Tileable-2048x2048-441212191)